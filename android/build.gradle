def keystorePropertiesFile = rootProject.file("../PrivateStuff/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    buildToolsVersion '28.0.3'
    compileSdkVersion 28
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }
    defaultConfig {
        applicationId "com.lyeeedar"
        minSdkVersion 14
        targetSdkVersion 28
        multiDexEnabled true
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            debuggable = false
        }
    }


    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }

    compileOptions.incremental = false
}
// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives() {
    doFirst {
        file("libs/armeabi/").mkdirs();
        file("libs/armeabi-v7a/").mkdirs();
        file("libs/arm64-v8a/").mkdirs();
        file("libs/x86_64/").mkdirs();
        file("libs/x86/").mkdirs();

        configurations.natives.files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
            if (jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
            if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
            if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
            if (outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

tasks.whenTaskAdded { packageTask ->
    if (packageTask.name.contains("package")) {
        packageTask.dependsOn 'copyAndroidNatives'
    }
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.lyeeedar/com.lyeeedar.AndroidLauncher'
}

task incrementVersion() {

    doLast {
        def manifestFile = new File('android/AndroidManifest.xml')
        def manifestContent = manifestFile.text

        def matches = manifestContent =~ ~/android:versionName="(.*)"/
        String version = matches[0][1]

        String[] split = version.split('\\.')
        def versionCode = split.join('')

        def last = split[split.length - 1]
        def asInt = last.toInteger()
        def newInt = asInt + 1
        split[split.length - 1] = newInt.toString()
        def newVersion = split.join('.')
        def newVersionCode = split.join('')
        manifestContent = manifestContent.replace('android:versionName="' + version + '"', 'android:versionName="' + newVersion + '"')
        manifestContent = manifestContent.replace('android:versionCode="' + versionCode + '"', 'android:versionCode="' + newVersionCode + '"')

        println "Old version: " + version
        println "New version: " + newVersion

        manifestFile.write manifestContent
    }
}

task doRelease() {

    ext.executeCmd = { cmd ->
        println cmd

        def proc = cmd.execute()
        def b = new StringBuffer()
        proc.consumeProcessErrorStream(b)

        def output = proc.text + b.toString()
        println output

        return output
    }

    doLast {
        String lastTag = executeCmd("git describe --tags --abbrev=0")
        String commitsSinceRelease = executeCmd("git log " + lastTag + "..HEAD --oneline")

        if (!commitsSinceRelease.trim()) {
            println "Work to release, continuing"

            def manifestFile = new File('android/AndroidManifest.xml')
            def manifestContent = manifestFile.text

            def matches = manifestContent =~ ~/android:versionName="(.*)"/
            String version = matches[0][1]

            def releaseFile = new File('android/build/outputs/bundle/release/android.aab')

            // push to playstore

            def post = new URL("https://www.googleapis.com/upload/androidpublisher/v3/applications/com.lyeeedar/edits/" + version + "/bundles").openConnection()
            post.setRequestMethod("POST")
            post.setDoOutput(true)
            post.setRequestProperty("Content-Type", "application/octet-stream")
            post.getOutputStream().write(releaseFile.bytes)
            def postRC = post.getResponseCode()
            println postRC

            if (postRC.equals(200)) {
                println(post.getInputStream().getText())

                // commit changes
                executeCmd("git add .")
                executeCmd('git commit -m"Bump version number and release"')
                executeCmd('git tag -a releases/' + version + ' -m "Release $version"')

                println "Release complete"
            } else {
                throw new GradleException('Something went wrong with releasing!')
            }
        } else {
            println "Release up to date"
        }
    }
}

// sets up the Android Idea project, using the old Ant based build.
idea {
    module {
        sourceDirs += file("src");
        scopes = [COMPILE: [plus: [project.configurations.compile]]]

        iml {
            withXml {
                def node = it.asNode()
                def builder = NodeBuilder.newInstance();
                builder.current = node;
                builder.component(name: "FacetManager") {
                    facet(type: "android", name: "Android") {
                        configuration {
                            option(name: "UPDATE_PROPERTY_FILES", value: "true")
                        }
                    }
                }
            }
        }
    }
}
dependencies {
}
